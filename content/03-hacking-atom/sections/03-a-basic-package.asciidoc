[[_package_word_count]]
=== Package: Word Count

Let's get started by writing a very simple package and looking at some of the tools needed to develop one effectively. We'll start by writing a package that tells you how many words are in the current buffer and display it in a small modal window.

[[_generate_package]]
==== Package Generator

The simplest way to start a package is to use the built-in package generator that ships with Atom. As you might expect by now, this generator is itself a separate package implemented in https://github.com/atom/package-generator[atom/package-generator].

You can run the generator by invoking the command palette and searching for "Generate Package". A dialog will appear asking you to name your new project. Atom will then create that directory and fill it out with a skeleton project and link it into your `.atom` directory so it's loaded when you launch your editor next time.

.Basic generated Atom package
image::images/package.png[package]

You can see that Atom has created about a dozen files that make up the package. Let's take a look at each of them to get an idea of how a package is structured, then we can modify them to get our word count functionality.

The basic package layout is as follows:

```text
my-package/
  grammars/
  keymaps/
  lib/
  menus/
  spec/
  snippets/
  styles/
  index.coffee
  package.json
```

Not every package will have (or need) all of these directories and the package generator doesn't create `snippets` or `grammars`. Let's see what some of these are so we can start messing with them.

[[_atom_package_json]]
===== package.json

Similar to http://en.wikipedia.org/wiki/Npm_(software)[npm packages], Atom packages contain a _package.json_ file in their top-level directory. This file contains metadata about the package, such as the path to its "main" module, library dependencies, and manifests specifying the order in which its resources should be loaded.

In addition to the regular https://docs.npmjs.com/files/package.json[npm package.json keys] available, Atom package.json files have their own additions.

- `main`: the path to the CoffeeScript file that's the entry point to your package. If this is missing, Atom will default to looking for an `index.coffee` or `index.js`.

- `styles`: an Array of Strings identifying the order of the
style sheets your package needs to load. If not specified, style sheets in the _styles_ directory are added alphabetically.

- `keymaps`: an Array of Strings identifying the order of the
key mappings your package needs to load. If not specified, mappings in the _keymaps_ directory are added alphabetically.

- `menus`: an Array of Strings identifying the order of the menu mappings your package needs to load. If not specified, mappings in the _menus_ directory are added alphabetically.

- `snippets`: an Array of Strings identifying the order of the
snippets your package needs to load. If not specified, snippets in the _snippets_ directory are added alphabetically.

- `activationCommands`: an Object identifying commands that trigger your package's activation. The keys are CSS selectors, the values are Arrays of Strings identifying the command.
The loading of your package is delayed until one of these events is triggered within the associated scope defined by the CSS selector.

- `activationHooks`: an Array of Strings identifying hooks that trigger your package's activation. The loading of your package is delayed until one of these hooks are triggered.
Known hooks are:
  - `language-package-name:grammar-used` (e.g) `language-javascript:grammar-used`

The `package.json` in the package we've just generated looks like this currently:

```js
{
  "name": "wordcount",
  "main": "./lib/wordcount",
  "version": "0.0.0",
  "description": "A short description of your package",
  "activationCommands": {
    "atom-workspace": "wordcount:toggle"
  },
  "repository": "https://github.com/atom/wordcount",
  "license": "MIT",
  "engines": {
    "atom": ">0.50.0"
  },
  "dependencies": {
  }
}
```

If you wanted to use activationHooks, you might have:

```js
{
  "name": "wordcount",
  "main": "./lib/wordcount",
  "version": "0.0.0",
  "description": "A short description of your package",
  "activationHooks": ["language-javascript:grammar-used", "language-coffee-script:grammar-used"],
  "repository": "https://github.com/atom/wordcount",
  "license": "MIT",
  "engines": {
    "atom": ">0.50.0"
  },
  "dependencies": {
  }
}
```

One of the first things you should do is ensure that this information is filled out. The name, description, repository URL the project will be at, and the license can all be filled out immediately. The other information we'll get into more detail on as we go.

===== Source Code

If you want to extend Atom's behavior, your package should contain a single top-level module, which you export from whichever file is indicated by the `main` key in your _package.json_ file. In the package we just generated, the main package file is `lib/wordcount.coffee`. The remainder of your code should be placed in the `lib` directory, and required from your top-level file. If the `main` key is not in your package.json file, it will look for `index.coffee` or `index.js` as the main entry point.

Your package's top-level module is a singleton object that manages the lifecycle of your extensions to Atom. Even if your package creates ten different views and appends them to different parts of the DOM, it's all managed from your top-level object.

Your package's top-level module should implement the following methods:

- `activate(state)`: This **required** method is called when your package is activated. It is passed the state data from the last time the window was serialized if your module implements the `serialize()` method. Use this to do initialization work when your package is started (like setting up DOM elements or binding events).

- `serialize()`: This **optional** method is called when the window is shutting down, allowing you to return JSON to represent the state of your component. When the window is later restored, the data you returned is passed to your module's `activate` method so you can restore your view to where the user left
off.

- `deactivate()`: This **optional** method is called when the window is shutting down. If your package is watching any files or holding external resources in any other way, release them here. If you're just subscribing to things on window, you don't need to worry because that's getting torn down anyway.

===== Style Sheets

Style sheets for your package should be placed in the _styles_ directory. Any style sheets in this directory will be loaded and attached to the DOM when your package is activated. Style sheets can be written as CSS or http://lesscss.org[Less], but Less is recommended.

Ideally, you won't need much in the way of styling. Atom provides a standard set of components which define both the colors and UI elements for any package that fits into Atom seamlessly. You can view all of Atom's UI components by opening the styleguide: open the command palette (`cmd-shift-P`) and search for _styleguide_, or just type `cmd-ctrl-shift-G`.

If you _do_ need special styling, try to keep only structural styles in the package style sheets. If you _must_ specify colors and sizing, these should be taken from the active theme's https://github.com/atom/atom-dark-ui/blob/master/styles/ui-variables.less[ui-variables.less].

An optional `styleSheets` array in your _package.json_ can list the style sheets by name to specify a loading order; otherwise, style sheets are loaded alphabetically.

[[_keybindings]]
===== Keymaps

It's recommended that you provide key bindings for commonly used actions for your extension, especially if you're also adding a new command. In our new package, we have a keymap filled in for us already in the `keymaps/wordcount.cson` file:

```coffeescript
'atom-workspace':
  'ctrl-alt-o': 'wordcount:toggle'
```

This means that if you hit `ctrl-alt-o`, our package will run the `toggle` command. We'll look at that code next, but if you want to change the default key mapping, you can do that in this file.

Keymaps are placed in the _keymaps_ subdirectory. By default, all keymaps are loaded in alphabetical order. An optional `keymaps` array in your _package.json_ can specify which keymaps to load and in what order.

Keybindings are executed by determining which element the keypress occurred on. In the example above, the `wordcount:toggle` command is executed when pressing `ctrl-alt-o` only on the `atom-workspace` element. This means that if you're focused on something else like the Tree View or Settings pane for example, this key mapping won't work.

We'll cover more advanced keymapping stuff a bit later in <<_keymaps_in_depth>>.

===== Menus

Menus are placed in the _menus_ subdirectory. This defines menu elements like what pops up when you right click (a context-menu) or would go in the menu bar (application menu) to trigger functionality in your plugin.

By default, all menus are loaded in alphabetical order. An optional `menus` array in your _package.json_ can specify which menus to load and in what order.

====== Application Menu

It's recommended that you create an application menu item for common actions with your package that aren't tied to a specific element. If we look in the `menus/wordcount.cson` file that was generated for us, we'll see a section that looks like this:

```coffeescript
'menu': [
  {
    'label': 'Packages'
    'submenu': [
      'label': 'Word Count'
      'submenu': [
        {
          'label': 'Toggle'
          'command': 'wordcount:toggle'
        }
      ]
    ]
  }
]
```

This section puts a ``Toggle'' menu item under a menu group named ``Word Count'' in the ``Packages'' menu.

.Application menu item
image::images/menu.png[application menu]

When you select that menu item, it will run the `wordcount:toggle` command, which we'll look at in a bit.

The menu templates you specify are merged with all other templates provided by other packages in the order which they were loaded.

====== Context Menu

It's recommended to specify a context menu item for commands that are linked to specific parts of the interface. In our `menus/wordcount.cson` file, we can see an auto-generated section that looks like this:

```coffeescript
'context-menu':
  'atom-text-editor': [
    {
      'label': 'Toggle Word Count'
      'command': 'wordcount:toggle'
    }
  ]
```

This adds a ``Toggle Word Count'' menu option to the menu that pops up when you right-click in an Atom text editor pane.

.Context menu entry
image::images/context-menu.png[context menu]

When you click that it will again run the `wordcount:toggle` method in your code.

Context menus are created by determining which element was selected and then adding all of the menu items whose selectors match that element (in the order which they were loaded). The process is then repeated for the elements until reaching the top of the DOM tree.

You can also add separators and submenus to your context menus. To add a submenu, provide a `submenu` key instead of a command. To add a separator, add an item with a single `type: 'separator'` key/value pair. For instance, you could do something like this:

```coffeescript
'context-menu':
  'atom-workspace': [
    {
      label: 'Text'
      submenu: [
        {label: 'Inspect Element', command: 'core:inspect'}
        {type: 'separator'}
        {label: 'Selector All', command: 'core:select-all'}
        {type: 'separator'}
        {label: 'Deleted Selected Text', command: 'core:delete'}
      ]
    }
  ]
```

==== Developing our Package

Currently with the generated package we have, if we run that `toggle` command through the menu or the command palette, we'll get a little pop up that says ``The Wordcount package is Alive! It's ALIVE!''.

.Wordcount Package is Alive dialog
image::images/toggle.png[word count is alive]

===== Understanding the Generated Code

Let's take a look at the code in our `lib` directory and see what is happening.

There are two files in our `lib` directory. One is the main file (`lib/wordcount.coffee`), which is pointed to in the `package.json` file as the main file to execute for this package. This file handles the logic of the whole plugin.

The second file is a View class (`lib/wordcount-view.coffee`), which handles the UI elements of the package. Let's look at this file first, since it's pretty simple.

```coffeescript
module.exports =
class WordcountView
  constructor: (serializedState) ->
    # Create root element
    @element = document.createElement('div')
    @element.classList.add('wordcount')

    # Create message element
    message = document.createElement('div')
    message.textContent = "The Wordcount package is Alive! It's ALIVE!"
    message.classList.add('message')
    @element.appendChild(message)

  # Returns an object that can be retrieved when package is activated
  serialize: ->

  # Tear down any state and detach
  destroy: ->
    @element.remove()

  getElement: ->
    @element
```

Basically the only thing happening here is that when the View class is created, it creates a simple `div` element and adds the `wordcount` class to it (so we can find or style it later) and then adds the ``Wordcount package is Alive!`` text to it. There is also a `getElement` method which returns that `div`. The `serialize` and `destroy` methods don't do anything and we won't have to worry about that until another example.

Notice that we're simply using the basic browser DOM methods (ie, `createElement()`, `appendChild()`).

The second file we have is the main entry point to the package (again, because it's referenced in the `package.json` file). Let's take a look at that file.

```coffeescript
WordcountView = require './wordcount-view'
{CompositeDisposable} = require 'atom'

module.exports = Wordcount =
  wordcountView: null
  modalPanel: null
  subscriptions: null

  activate: (state) ->
    @wordcountView = new WordcountView(state.wordcountViewState)
    @modalPanel = atom.workspace.addModalPanel(item: @wordcountView.getElement(), visible: false)

    # Events subscribed to in atom's system can be easily cleaned up with a CompositeDisposable
    @subscriptions = new CompositeDisposable

    # Register command that toggles this view
    @subscriptions.add atom.commands.add 'atom-workspace',
      'wordcount:toggle': => @toggle()

  deactivate: ->
    @modalPanel.destroy()
    @subscriptions.dispose()
    @wordcountView.destroy()

  serialize: ->
    wordcountViewState: @wordcountView.serialize()

  toggle: ->
    console.log 'Wordcount was toggled!'

    if @modalPanel.isVisible()
      @modalPanel.hide()
    else
      @modalPanel.show()
```

There is a bit more going on here.  First of all we can see that we are defining four methods. The only required one is `activate`. The `deactivate` and `serialize` methods are expected by Atom but optional. The `toggle` method is one Atom is not looking for, so we'll have to invoke it somewhere for it to be called, which you may recall we do both in the `activationCommands` section of the `package.json` file and in the action we have in the menu file.

The `deactivate` method simply destroys the various class instances we've created and the `serialize` method simply passes on the serialization to the View class. Nothing too exciting here.

The `activate` command does a number of things. For one, it is not called automatically when Atom starts up, it is first called when one of the `activationCommands` as defined in the `package.json` file are called. In this case, `activate` is only called the first time the `toggle` command is called. If nobody ever invokes the menu item or hotkey, this code is never called.

This method does two things. The first is that it creates an instance of the View class we have and adds the element that it creates to a hidden modal panel in the Atom workspace.

```coffeescript
@wordcountView = new WordcountView(state.wordcountViewState)
@modalPanel = atom.workspace.addModalPanel(
                  item: @wordcountView.getElement(),
                  visible: false
              )
```

We'll ignore the state stuff for now, since it's not important for this simple plugin. The rest should be fairly straightforward.

The next thing this method does is create an instance of the CompositeDisposable class so it can register all the commands that can be called from the plugin so other plugins could subscribe to these events.

```coffeescript
# Events subscribed to in atom's system can be easily cleaned up with a CompositeDisposable
@subscriptions = new CompositeDisposable

# Register command that toggles this view
@subscriptions.add atom.commands.add 'atom-workspace', 'wordcount:toggle': => @toggle()
```

Next we have the `toggle` method. This method simply toggles the visibility of the modal panel that we created in the `activate` method.

```coffeescript
toggle: ->
  console.log 'Wordcount was toggled!'

  if @modalPanel.isVisible()
    @modalPanel.hide()
  else
    @modalPanel.show()
```

This should be fairly simple to understand. We're looking to see if the modal element is visible and hiding or showing it depending on it's current state.

===== The Flow

So, let's review the actual flow in this package.

* Atom starts up
* Atom reads the package.json
* Atom reads keymaps, menus, main file
* User runs a package command
* Atom executes the `activate` method
  ** Creates a WordCount view, which creates a div
  ** Grabs that div and sticks it in a hidden modal
* Atom executes the package command
  ** Sees that the modal is hidden, makes it visible
* User runs a package command again
* Atom executes the package command
  ** Sees that the modal is visible, makes it hidden
* You shut down Atom
  ** Serializations?

TODO: Verify this and perhaps make it a graphic?

[[_counting_words]]
===== Counting the Words

So now that we understand what is happening, let's modify the code so that our little modal box shows us the current word count instead of static text.

We'll do this in a very simple way. When the dialog is toggled, we'll count the words right before displaying the modal. So let's do this in the `toggle` command.  If we add some code to count the words and ask the view to update itself, we'll have something like this:

```coffeescript
toggle: ->
  if @modalPanel.isVisible()
    @modalPanel.hide()
  else
    editor = atom.workspace.getActiveTextEditor()
    words = editor.getText().split(/\s+/).length
    @wordcountView.setCount(words)
    @modalPanel.show()
```

Let's look at the 3 lines we've added. First we get an instance of the current editor object (where our text to count is) by calling https://atom.io/docs/api/latest/Workspace#instance-getActiveTextEditor[`atom.workspace.getActiveTextEditor()`].

Next we get the number of words by calling https://atom.io/docs/api/v0.179.0/TextEditor#instance-getText[`getText()`] on our new editor object, then splitting that text on whitespace with a regular expression and then getting the length of that array.

Finally, we tell our view to update the word count it displays by calling the `setCount()` method on our view and then showing the modal again. Since that method doesn't yet exist, let's create it now.

We can add this code to the end of our `wordcount-view.coffee` file:


```coffeescript
setCount: (count) ->
  displayText = "There are #{count} words."
  @element.children[0].textContent = displayText
```

Pretty simple - we take the count number that was passed in and place it into a string that we then stick into the element that our view is controlling.

.Word Count Working
image::images/wordcount.png[word count working]

==== Basic Debugging

You'll notice a few `console.log` statements in the code. One of the cool things about Atom being built on Chromium is that you can use some of the same debugging tools available to you that you have when doing web development.

To open up the Developer Console, hit `alt-cmd-I`, or choose the menu option `View > Developer > Toggle Developer Tools`.

.Developer Tools Debugging
image::images/dev-tools.png[dev tools debugging]

From here you can inspect objects, run code and view console output just as though you were debugging a web site.

==== Testing

Your package should have tests, and if they're placed in the _spec_ directory, they can be run by Atom.

Under the hood, http://jasmine.github.io[Jasmine] executes your tests, so you can assume that any DSL available there is also available to your package.

===== Running Tests

Once you've got your test suite written, you can run it by pressing `cmd-alt-ctrl-p` or via the _Developer > Run Package Specs_ menu. Our generated package comes with an example test suite, so you can run this right now to see what happens.

.Spec Suite Results
image::images/spec-suite.png[spec suite results]

You can also use the `apm test` command to run them from the command line. It prints the test output and results to the console and returns the proper status code depending on whether the tests passed or failed.

===== Making our Tests Work

As you can see, our generated tests are all failing because of the changes we've made.

TODO: Fix the tests

[[_atom_publishing]]
==== Publishing

Now that our simple plugin is working and tested, let's go ahead and publish it so it's available to the world.

Atom bundles a command line utility called `apm` which we first used back in <<_atom_command_line>> to search for and install packages via the command line. The `apm` command can also be used to publish Atom packages to the public registry and update them.

===== Prepare Your Package

There are a few things you should double check before publishing:

  * Your *package.json* file has `name`, `description`, and `repository` fields.
  * Your *package.json* file has a `version` field with a value of  `"0.0.0"`.
  * Your *package.json* file has an `engines` field that contains an entry
    for Atom such as: `"engines": {"atom": ">=0.50.0"}`.
  * Your package has a `README.md` file at the root.
  * Your package is in a Git repository that has been pushed to https://github.com[GitHub]. Follow http://guides.github.com/overviews/desktop[this guide] if your package isn't already on GitHub.

===== Publish Your Package

Before you publish a package it is a good idea to check ahead of time if a package with the same name has already been published to https://atom.io[atom.io]. You can do that by visiting `https://atom.io/packages/my-package` to see if the package already exists. If it does, update your package's name to something that is available before proceeding.

Now let's review what the `apm publish` command does:

  1. Registers the package name on atom.io if it is being published for the first time.
  2. Updates the `version` field in the *package.json* file and commits it.
  3. Creates a new http://git-scm.com/book/en/Git-Basics-Tagging[Git tag] for the version being published.
  4. Pushes the tag and current branch up to GitHub.
  5. Updates atom.io with the new version being published.

Now run the following commands to publish your package:

```sh
cd ~/github/my-package
apm publish minor
```

If this is the first package you are publishing, the `apm publish` command may prompt you for your GitHub username and password. This is required to publish and you only need to enter this information the first time you publish. The credentials are stored securely in your https://en.wikipedia.org/wiki/Keychain_(Apple)[keychain] once you login.

Your package is now published and available on atom.io. Head on over to `https://atom.io/packages/my-package` to see your package's page.

With `apm publish`, you can bump the version and publish by using

```sh
apm publish <version-type>
```
where `<version-type>` can be `major`, `minor` and `patch`.

The `major` option to the publish command tells apm to increment the first digit of the version before publishing so the published version will be `1.0.0` and the Git tag created will be `v1.0.0`.

The `minor` option to the publish command tells apm to increment the second digit of the version before publishing so the published version will be `0.1.0` and the Git tag created will be `v0.1.0`.

The `patch` option to the publish command tells apm to increment the third digit of the version before publishing so the published version will be `0.0.1` and the Git tag created will be `v0.0.1`.

Use `major` when you make a change that breaks backwards compatibility, like changing defaults or removing features. Use `minor` when adding new functionality or making improvements on existing code. Use `patch` when you fix a bug that was causing incorrect behaviour.

Check out http://semver.org[semantic versioning] to learn more about versioning your package releases.

You can also run `apm help publish` to see all the available options and `apm help` to see all the other available commands.

==== Summary

We've now generated, customized and published our first plugin for Atom. Congratulations! Now anyone can install our masterpiece from directly within Atom as we did in <<_atom_packages>>.
